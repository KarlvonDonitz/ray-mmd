texture _EyeLumMap : RENDERCOLORTARGET<
	int2 Dimensions = {256, 256};
	string Format = "R32F";
	int Miplevels = 0;
>;
texture _EyeLumAveMap : RENDERCOLORTARGET<
	int2 Dimensions = {1, 1};
	string Format = "R32F";
>;
sampler _EyeLumMap_LinearSampler = sampler_state {
	texture = <_EyeLumMap>;
	MinFilter = Linear; MagFilter = Linear; MipFilter = Linear;
	AddressU = CLAMP; AddressV = CLAMP;
};
sampler _EyeLumAveMap_PointSampler = sampler_state {
	texture = <_EyeLumAveMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

float CalcAdaptedLum(float adaptedLum, float lum, float delta)
{
    return max(0, adaptedLum + (lum - adaptedLum) * (1.0f - exp2(-1.45 * delta)));
}

float ComputeExposureISO(float avgLum, float middleGray = 12.5)
{
	float EV100 = log2(avgLum * 100.0f / middleGray);
	float maxLuminance = 1.2f * exp2(EV100);
	float exposure = 0.5 / maxLuminance; // mExposureMin = 2.0, Need to multiply with 0.5
	return clamp(exposure, mExposureEyeAdapationMin, mExposureEyeAdapationMax);
}

void EyeDownsampleVS(
	in float4 Position : POSITION,
	in float2 Texcoord : TEXCOORD,
	out float4 oTexcoord1 : TEXCOORD0,
	out float4 oTexcoord2 : TEXCOORD1,
	out float4 oPosition : POSITION,
	uniform float2 _MainTex_TexelSize)
{
	oTexcoord1 = Texcoord.xyxy + _MainTex_TexelSize.xyxy * 0.5f;
	oTexcoord1.xy = oTexcoord1.xy + float2(1.0f / 512.0f, 0);
	oTexcoord2.xy = oTexcoord1.xy + float2(1.0f / 512.0f, 1.0f / 512.0f);
	oTexcoord2.zw = oTexcoord1.xy + float2(0, 1.0f / 512.0f);
	oPosition = Position;
}

float4 EyeDownsamplePS(
	in float4 uv0 : TEXCOORD0,
	in float4 uv1 : TEXCOORD1,
	uniform sampler _MainTex) : COLOR
{
	float color = log(max(luminance(tex2Dlod(_MainTex, float4(uv0.xy, 0, 0)).rgb), 0.02));
	color += log(max(luminance(tex2Dlod(_MainTex, float4(uv0.zw, 0, 0)).rgb), 0.02));
	color += log(max(luminance(tex2Dlod(_MainTex, float4(uv1.xy, 0, 0)).rgb), 0.02));
	color += log(max(luminance(tex2Dlod(_MainTex, float4(uv1.zw, 0, 0)).rgb), 0.02));
	color *= 0.25;

	return color;
}

float4 EyeAdapationPS(in float2 coord: TEXCOORD0) : COLOR
{
	float lum = exp(tex2Dlod(_EyeLumMap_LinearSampler, float4(coord, 0, 20)).r);
	float adaptedLum = tex2Dlod(_EyeLumAveMap_PointSampler, float4(float2(0.5, 0.5), 0, 0)).r;

	if (!all(isfinite(lum)))
		lum = 1.0f;

	if (!all(isfinite(adaptedLum)))
		adaptedLum = 1.0f;

	return CalcAdaptedLum(adaptedLum, lum, elapsed);
}