#include "../../ray.conf"
#include "../../ray_advanced.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"

float mEnvRotateX : CONTROLOBJECT<string name="(self)"; string item = "EnvRotateX";>;
float mEnvRotateY : CONTROLOBJECT<string name="(self)"; string item = "EnvRotateY";>;
float mEnvRotateZ : CONTROLOBJECT<string name="(self)"; string item = "EnvRotateZ";>;
float mFogColorHP :  CONTROLOBJECT<string name="(self)"; string item = "FogColorH+";>;
float mFogColorSP :  CONTROLOBJECT<string name="(self)"; string item = "FogColorS+";>;
float mFogColorVP :  CONTROLOBJECT<string name="(self)"; string item = "FogColorV+";>;
float mFogColorVM :  CONTROLOBJECT<string name="(self)"; string item = "FogColorV-";>;
float mFogRangeP : CONTROLOBJECT<string name="(self)"; string item = "FogRange+";>;
float mFogRangeM : CONTROLOBJECT<string name="(self)"; string item = "FogRange-";>;
float mFogDensityP : CONTROLOBJECT<string name="(self)"; string item = "FogDensity+";>;
float mFogDensityM : CONTROLOBJECT<string name="(self)"; string item = "FogDensity-";>;
float mFogMipNearP : CONTROLOBJECT<string name="(self)"; string item = "FogMipNear+";>;
float mFogMipNearM : CONTROLOBJECT<string name="(self)"; string item = "FogMipNear-";>;
float mFogMipFarP : CONTROLOBJECT<string name="(self)"; string item = "FogMipFar+";>;
float mFogMipFarM : CONTROLOBJECT<string name="(self)"; string item = "FogMipFar-";>;

static const float FogMipFar = lerp(lerp(mFogFarLimit.x, mFogFarLimit.z, mFogMipFarP), mFogFarLimit.y, mFogMipFarM);
static const float FogMipNear = lerp(lerp(mFogNearLimit.x, mFogNearLimit.z, mFogMipNearP), mFogNearLimit.y, mFogMipNearM);
static const float FogRange = lerp(lerp(mFogRangeLimit.x, mFogRangeLimit.y, mFogRangeP), mFogRangeLimit.z, mFogRangeM);
static const float FogDensity = lerp(lerp(mFogDensityLimit.x, mFogDensityLimit.y, mFogDensityP), mFogDensityLimit.z, mFogDensityM);
static const float3 FogColor = hsv2rgb(float3(mFogColorHP, mFogColorSP, lerp(lerp(1, 2, mFogColorVP), 0, mFogColorVM)));
static const float3x3 matTransform = CreateRotate(float3(mEnvRotateX, mEnvRotateY, mEnvRotateZ) * PI_2);

#if SKYBOX_FOG_ENABLE
texture MipFogMap<string ResourceName = SKYBOX_FOG_MAP_FILE;>;
sampler MipFogMapSamp = sampler_state {
	texture = <MipFogMap>;
	MINFILTER = LINEAR; MAGFILTER = LINEAR; MIPFILTER = LINEAR;
	ADDRESSU = CLAMP; ADDRESSV = CLAMP;
};
#endif

float ComputeFogMip(float near, float far, float linearDepth, float mipLevel)
{
	return (1 - saturate((linearDepth - near) / (far - near))) * mipLevel;
}

float ComputeMipFogFactor(float z)
{
#if SKYBOX_FOG_TYPE == 0 || SKYBOX_FOG_TYPE == 1
    // factor = exp(-(density*z)^2)
    // -density * z computed at vertex
    return float(z * FogDensity);
#elif SKYBOX_FOG_TYPE == 2
    // factor = (end-z)/(end-start) = z * (-1/(end-start)) + (end/(end-start))
    float fogFactor = saturate(z * _MipFogFactorParams.z + _MipFogFactorParams.w);
    return float(fogFactor);
#else
    return 0.0;
#endif
}

float ComputeMipFogIntensity(float fogFactor)
{
    float fogIntensity = 0.0;
#if SKYBOX_FOG_TYPE == 0
    fogIntensity = saturate(exp2(-fogFactor));
#elif SKYBOX_FOG_TYPE == 1
    fogIntensity = saturate(exp2(-fogFactor * fogFactor));
#elif SKYBOX_FOG_TYPE == 2
    fogIntensity = fogFactor;
#endif
    return fogIntensity;
}

void ImageBasedFogVertex(
	in float4 Position : POSITION,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oPosition : POSITION)
{
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
}

float4 ImageBasedFogFragment(in float4 texcoord : TEXCOORD0) : COLOR0
{
	float2 coord = texcoord.xy / texcoord.w;

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, material);

	float fogFactor = ComputeMipFogFactor(material.linearDepth);
	float fogIntensity = ComputeMipFogIntensity(fogFactor);

	float mipFogFactor = 1.0h - fogIntensity;
#if SKYBOX_FOG_DISCARD_SKY
	mipFogFactor *= step(0, sum(material.albedo + material.specular) - 1e-5);
#endif

	float3 mipFogColor = FogColor;

#if SKYBOX_FOG_ENABLE
	float3 worldNormal = mul(material.normal, (float3x3)matViewInverse);
	float3 normal = normalize(mul(matTransform, normalize(worldNormal)));
	float mipLevel = ComputeFogMip(FogMipNear, FogMipFar, material.linearDepth, SKYBOX_FOG_MIPMAP_LEVEL);
	mipFogColor *= DecodeRGBT(tex2Dlod(MipFogMapSamp, float4(ComputeSphereCoord(-normal), 0, mipLevel)));
#endif

	return float4(mipFogColor, mipFogFactor);
}

#define OBJECT_TEC(name, mmdpass)\
	technique name<string MMDPass = mmdpass;\
	> {\
		pass DrawObject {\
			ZEnable = false; ZWriteEnable = false;\
			AlphaBlendEnable = true; AlphaTestEnable = FALSE;\
			SrcBlend = ONE; DestBlend = ONE;\
			VertexShader = compile vs_3_0 ImageBasedFogVertex();\
			PixelShader  = compile ps_3_0 ImageBasedFogFragment();\
		}\
	}

OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTec1, "object_ss")

technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTec<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}