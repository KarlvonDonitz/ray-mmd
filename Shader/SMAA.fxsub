/**
 * Copyright (C) 2013 Jorge Jimenez (jorge@iryoku.com)
 * Copyright (C) 2013 Jose I. Echevarria (joseignacioechevarria@gmail.com)
 * Copyright (C) 2013 Belen Masia (bmasia@unizar.es)
 * Copyright (C) 2013 Fernando Navarro (fernandn@microsoft.com)
 * Copyright (C) 2013 Diego Gutierrez (diegog@unizar.es)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
 * of the Software, and to permit persons to whom the Software is furnished to
 * do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software. As clarification, there
 * is no requirement that the copyright notice and permission be included in
 * binary distributions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
#if AA_QUALITY == 2 || AA_QUALITY == 4
	#define SMAA_THRESHOLD 0.1
	#define SMAA_MAX_SEARCH_STEPS 16
	#define SMAA_MAX_SEARCH_STEPS_DIAG 8
	#define SMAA_CORNER_ROUNDING 25
	#define SMAA_CORNER_ROUNDING_NORM (float(SMAA_CORNER_ROUNDING) / 100.0)
#else
	#define SMAA_THRESHOLD 0.05
	#define SMAA_MAX_SEARCH_STEPS 32
	#define SMAA_MAX_SEARCH_STEPS_DIAG 16
	#define SMAA_CORNER_ROUNDING 25
	#define SMAA_CORNER_ROUNDING_NORM (float(SMAA_CORNER_ROUNDING) / 100.0)
#endif

#define SMAA_AREATEX_MAX_DISTANCE 16
#define SMAA_AREATEX_MAX_DISTANCE_DIAG 20
#define SMAA_AREATEX_PIXEL_SIZE (1.0 / float2(160.0, 560.0))
#define SMAA_AREATEX_SUBTEX_SIZE (1.0 / 7.0)
#define SMAA_SEARCHTEX_SIZE float2(66.0, 33.0)
#define SMAA_SEARCHTEX_PACKED_SIZE float2(64.0, 16.0)
#define SMAA_PREDICATION_SCALE 2.0
#define SMAA_PREDICATION_THRESHOLD 0.01
#define SMAA_PREDICATION_STRENGTH 0.4

texture _SMAAEdgeMap : RENDERCOLORTARGET <
	float2 ViewPortRatio = {1.0, 1.0};
	bool AntiAlias = false;
	string Format = "A8R8G8B8";
>;
texture _SMAABlendMap : RENDERCOLORTARGET <
	float2 ViewPortRatio = {1.0, 1.0};
	bool AntiAlias = false;
	string Format = "A8R8G8B8";
>;
sampler _SMAAEdgeMap_LinearSampler = sampler_state {
	texture = <_SMAAEdgeMap>;
	MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE;
	AddressU = CLAMP;  AddressV = CLAMP;
};
sampler _SMAAEdgeMap_PointSampler = sampler_state {
	texture = <_SMAAEdgeMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP;  AddressV = CLAMP;
};
sampler _SMAABlendMap_PointSampler = sampler_state {
	texture = <_SMAABlendMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP;  AddressV = CLAMP;
};
texture _SMAAAreaMap<string ResourceName = "shader/textures/smaa_area.dds";>;
sampler _SMAAAreaMap_PointSampler = sampler_state
{
	texture = <_SMAAAreaMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};
texture _SMAASearchMap<string ResourceName = "shader/textures/smaa_search.dds";>;
sampler _SMAASearchMap_PointSampler = sampler_state
{
	texture = <_SMAASearchMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

float3 SMAAGatherNeighbours(float2 uv, float4 offset[3], sampler _MainTex) 
{
	float P     = tex2Dlod(_MainTex, float4(uv, 0, 0)).r;
	float Pleft = tex2Dlod(_MainTex, float4(offset[0].xy, 0, 0)).r;
	float Ptop  = tex2Dlod(_MainTex, float4(offset[0].zw, 0, 0)).r;
	return float3(P, Pleft, Ptop);
}

float2 SMAACalculatePredicatedThreshold(float2 uv, float4 offset[3], sampler _MainTex) 
{
    float3 neighbours = SMAAGatherNeighbours(uv, offset, _MainTex);
    float2 delta = abs(neighbours.xx - neighbours.yz);
    float2 edges = step(SMAA_PREDICATION_THRESHOLD, delta);
    return SMAA_PREDICATION_SCALE * SMAA_THRESHOLD * (1.0 - SMAA_PREDICATION_STRENGTH * edges);
}

float SMAASearchLength(sampler searchTex, float2 e, float offset) 
{
	float2 scale = SMAA_SEARCHTEX_SIZE * float2(0.5, -1.0) + float2(-1.0,  1.0);
	float2 bias = SMAA_SEARCHTEX_SIZE * float2(offset, 1.0) + float2( 0.5, -0.5);

	scale *= 1.0 / SMAA_SEARCHTEX_PACKED_SIZE;
	bias *= 1.0 / SMAA_SEARCHTEX_PACKED_SIZE;

	return tex2Dlod(searchTex, float4(mad(scale, e, bias), 0, 0)).r;
}

float SMAASearchXLeft(sampler edgesTex, sampler searchTex, float2 texcoord, float end, float2 _MainTex_TexelSize)
{
	float2 e = float2(0.0, 1.0);

	while (texcoord.x > end && e.g > 0.8281 && e.r == 0.0)
	{
		e = tex2Dlod(edgesTex, float4(texcoord, 0, 0)).rg;
		texcoord -= _MainTex_TexelSize * float2(2.0, 0.0);
	}

	float offset = mad(-(255.0 / 127.0), SMAASearchLength(searchTex, e, 0.0), 3.25);
	return mad(_MainTex_TexelSize.x, offset, texcoord.x);
}

float SMAASearchXRight(sampler edgesTex, sampler searchTex, float2 texcoord, float end, float2 _MainTex_TexelSize) 
{
	float2 e = float2(0.0, 1.0);

	while (texcoord.x < end && e.g > 0.8281 && e.r == 0.0)
	{
		if (!(texcoord.x < end &&  e.g > 0.8281 && e.r == 0.0)) break;
		e = tex2Dlod(edgesTex, float4(texcoord, 0, 0)).rg;
		texcoord += _MainTex_TexelSize * float2(2.0, 0.0);
	}

	float offset = mad(-(255.0 / 127.0), SMAASearchLength(searchTex, e, 0.5), 3.25);
	return mad(-_MainTex_TexelSize.x, offset, texcoord.x);
}

float SMAASearchYUp(sampler edgesTex, sampler searchTex, float2 texcoord, float end, float2 _MainTex_TexelSize) 
{
	float2 e = float2(1.0, 0.0);

	while (texcoord.y > end && e.r > 0.8281 && e.g == 0.0)
	{
		e = tex2Dlod(edgesTex, float4(texcoord, 0, 0)).rg;
		texcoord -= _MainTex_TexelSize * float2(0.0, 2.0);
	}

	float offset = mad(-(255.0 / 127.0), SMAASearchLength(searchTex, e.gr, 0.0), 3.25);
	return mad(_MainTex_TexelSize.y, offset, texcoord.y);
}

float SMAASearchYDown(sampler edgesTex, sampler searchTex, float2 texcoord, float end, float2 _MainTex_TexelSize) 
{
	float2 e = float2(1.0, 0.0);

	while (texcoord.y < end && e.r > 0.8281 && e.g == 0.0)
	{
		e = tex2Dlod(edgesTex, float4(texcoord, 0, 0)).rg;
		texcoord += _MainTex_TexelSize * float2(0.0, 2.0);
	}

	float offset = mad(-(255.0 / 127.0), SMAASearchLength(searchTex, e.gr, 0.5), 3.25);
	return mad(-_MainTex_TexelSize.y, offset, texcoord.y);
}

float2 SMAAArea(sampler areaTex, float2 dist, float e1, float e2, float offset) 
{
	float2 texcoord = mad(float2(SMAA_AREATEX_MAX_DISTANCE, SMAA_AREATEX_MAX_DISTANCE), round(4.0 * float2(e1, e2)), dist);
	texcoord = mad(SMAA_AREATEX_PIXEL_SIZE, texcoord, 0.5 * SMAA_AREATEX_PIXEL_SIZE);
	texcoord.y = mad(SMAA_AREATEX_SUBTEX_SIZE, offset, texcoord.y);
	return tex2Dlod(areaTex, float4(texcoord, 0, 0)).ra;
}

void SMAAMovc(bool2 cond, inout float2 variable, float2 value) 
{
	[flatten] if (cond.x) variable.x = value.x;
	[flatten] if (cond.y) variable.y = value.y;
}

void SMAAMovc(bool4 cond, inout float4 variable, float4 value) 
{
	SMAAMovc(cond.xy, variable.xy, value.xy);
	SMAAMovc(cond.zw, variable.zw, value.zw);
}

#if SMAA_MAX_SEARCH_STEPS_DIAG

float2 SMAAAreaDiag(sampler areaTex, float2 dist, float2 e, float offset) 
{
	float2 texcoord = mad(float2(SMAA_AREATEX_MAX_DISTANCE_DIAG, SMAA_AREATEX_MAX_DISTANCE_DIAG), e, dist);
	texcoord = mad(SMAA_AREATEX_PIXEL_SIZE, texcoord, 0.5 * SMAA_AREATEX_PIXEL_SIZE);
	texcoord.x += 0.5;
	texcoord.y += SMAA_AREATEX_SUBTEX_SIZE * offset;
	return tex2Dlod(areaTex, float4(texcoord, 0, 0)).ra;
}

float2 SMAADecodeDiagBilinearAccess(float2 e) 
{
	e.r = e.r * abs(5.0 * e.r - 5.0 * 0.75);
	return round(e);
}

float4 SMAADecodeDiagBilinearAccess(float4 e) 
{
	e.rb = e.rb * abs(5.0 * e.rb - 5.0 * 0.75);
	return round(e);
}

float2 SMAASearchDiag1(sampler edgesTex, float2 texcoord, float2 dir, float2 _MainTex_TexelSize, out float2 e)
{
	float4 coord = float4(texcoord, -1.0, 1.0);
	float3 t = float3(_MainTex_TexelSize.xy, 1.0);

	for (int i = 0; i < SMAA_MAX_SEARCH_STEPS_DIAG; i++)
	{
		if (!(coord.z < float(SMAA_MAX_SEARCH_STEPS_DIAG - 1) && coord.w > 0.9)) break;
		coord.xyz = mad(t, float3(dir, 1.0), coord.xyz);
		e = tex2Dlod(edgesTex, float4(coord.xy, 0, 0)).rg;
		coord.w = dot(e, float2(0.5, 0.5));
	}

	return coord.zw;
}

float2 SMAASearchDiag2(sampler edgesTex, float2 texcoord, float2 dir, float2 _MainTex_TexelSize, out float2 e)
{
	float4 coord = float4(texcoord, -1.0, 1.0);
	coord.x += 0.25 * _MainTex_TexelSize.x;
	float3 t = float3(_MainTex_TexelSize.xy, 1.0);

	for (int i = 0; i < SMAA_MAX_SEARCH_STEPS_DIAG; i++)
	{
		if (!(coord.z < float(SMAA_MAX_SEARCH_STEPS_DIAG - 1) && coord.w > 0.9)) break;
		coord.xyz = mad(t, float3(dir, 1.0), coord.xyz);
		e = tex2Dlod(edgesTex, float4(coord.xy, 0, 0)).rg;
		e = SMAADecodeDiagBilinearAccess(e);
		coord.w = dot(e, float2(0.5, 0.5));
	}

	return coord.zw;
}

float2 SMAACalculateDiagWeights(sampler edgesTex, sampler areaTex, float2 texcoord, float2 _MainTex_TexelSize, float2 e, float4 subsampleIndices) 
{
	float2 weights = float2(0.0, 0.0);

	float4 d;
	float2 end;
	if (e.r > 0.0) 
	{
		d.xz = SMAASearchDiag1(edgesTex, texcoord, float2(-1.0,  1.0), _MainTex_TexelSize, end);
		d.x += float(end.y > 0.9);
	}
	else
	{
		d.xz = float2(0.0, 0.0);
	}

	d.yw = SMAASearchDiag1(edgesTex, texcoord, float2(1.0, -1.0), _MainTex_TexelSize, end);

	[branch]
	if (d.x + d.y > 2.0) 
	{
		float4 coords = mad(float4(-d.x + 0.25, d.x, d.y, -d.y - 0.25), _MainTex_TexelSize.xyxy, texcoord.xyxy);
		
		float4 c;
		c.xy = tex2Dlod(edgesTex, float4(coords.xy + float2(-1,  0) * _MainTex_TexelSize, 0, 0)).rg;
		c.zw = tex2Dlod(edgesTex, float4(coords.zw + float2( 1,  0) * _MainTex_TexelSize, 0, 0)).rg;
		c.yxwz = SMAADecodeDiagBilinearAccess(c.xyzw);
		
		float2 cc = mad(float2(2.0, 2.0), c.xz, c.yw);
		SMAAMovc(bool2(step(0.9, d.zw)), cc, float2(0.0, 0.0));
		
		weights += SMAAAreaDiag(areaTex, d.xy, cc, subsampleIndices.z);
	}

	d.xz = SMAASearchDiag2(edgesTex, texcoord, float2(-1.0, -1.0), _MainTex_TexelSize, end);
	if (tex2Dlod(edgesTex, float4(texcoord + float2(1, 0) * _MainTex_TexelSize, 0, 0)).r > 0.0) 
	{
		d.yw = SMAASearchDiag2(edgesTex, texcoord, float2(1.0, 1.0), _MainTex_TexelSize, end);
		d.y += float(end.y > 0.9);
	}
	else
	{
		d.yw = float2(0.0, 0.0);
	}

	[branch]
	if (d.x + d.y > 2.0)
	{
		float4 coords = mad(float4(-d.x, -d.x, d.y, d.y), _MainTex_TexelSize.xyxy, texcoord.xyxy);
		float4 c;
		c.x  = tex2Dlod(edgesTex, float4(coords.xy + float2(-1,  0) * _MainTex_TexelSize, 0, 0)).g;
		c.y  = tex2Dlod(edgesTex, float4(coords.xy + float2( 0, -1) * _MainTex_TexelSize, 0, 0)).r;
		c.zw = tex2Dlod(edgesTex, float4(coords.zw + float2( 1,  0) * _MainTex_TexelSize, 0, 0)).gr;
		
		float2 cc = mad(float2(2.0, 2.0), c.xz, c.yw);
		SMAAMovc(bool2(step(0.9, d.zw)), cc, float2(0.0, 0.0));

		weights += SMAAAreaDiag(areaTex, d.xy, cc, subsampleIndices.w).gr;
	}

	return weights;
}
#endif

void SMAADetectHorizontalCornerPattern(sampler edgesTex, inout float2 weights, float4 texcoord, float2 d, float2 _MainTex_TexelSize) 
{
#if SMAA_CORNER_ROUNDING
	float2 leftRight = step(d.xy, d.yx);
	float2 rounding = (1.0 - SMAA_CORNER_ROUNDING_NORM) * leftRight;

	rounding /= leftRight.x + leftRight.y;

	float2 factor = float2(1.0, 1.0);
	factor.x -= rounding.x * tex2Dlod(edgesTex, float4(texcoord.xy + float2(0,  1) * _MainTex_TexelSize, 0, 0)).r;
	factor.x -= rounding.y * tex2Dlod(edgesTex, float4(texcoord.zw + float2(1,  1) * _MainTex_TexelSize, 0, 0)).r;
	factor.y -= rounding.x * tex2Dlod(edgesTex, float4(texcoord.xy + float2(0, -2) * _MainTex_TexelSize, 0, 0)).r;
	factor.y -= rounding.y * tex2Dlod(edgesTex, float4(texcoord.zw + float2(1, -2) * _MainTex_TexelSize, 0, 0)).r;

	weights *= saturate(factor);
#endif
}

void SMAADetectVerticalCornerPattern(sampler edgesTex, inout float2 weights, float4 texcoord, float2 d, float2 _MainTex_TexelSize) 
{
#if SMAA_CORNER_ROUNDING
	float2 leftRight = step(d.xy, d.yx);
	float2 rounding = (1.0 - SMAA_CORNER_ROUNDING_NORM) * leftRight;

	rounding /= leftRight.x + leftRight.y;

	float2 factor = float2(1.0, 1.0);
	factor.x -= rounding.x * tex2Dlod(edgesTex, float4(texcoord.xy + float2( 1, 0) * _MainTex_TexelSize, 0, 0)).g;
	factor.x -= rounding.y * tex2Dlod(edgesTex, float4(texcoord.zw + float2( 1, 1) * _MainTex_TexelSize, 0, 0)).g;
	factor.y -= rounding.x * tex2Dlod(edgesTex, float4(texcoord.xy + float2(-2, 0) * _MainTex_TexelSize, 0, 0)).g;
	factor.y -= rounding.y * tex2Dlod(edgesTex, float4(texcoord.zw + float2(-2, 1) * _MainTex_TexelSize, 0, 0)).g;

	weights *= saturate(factor);
#endif
}

float4 SMAAEdgeDetectionVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float4 oTexcoord2 : TEXCOORD2,
	out float4 oTexcoord3 : TEXCOORD3,
	uniform float2 _MainTex_TexelSize) : POSITION
{
	oTexcoord0 = Texcoord.xyxy + _MainTex_TexelSize.xyxy * 0.5;
	oTexcoord1 = mad(_MainTex_TexelSize.xyxy, float4(-1.0, 0.0, 0.0, -1.0), oTexcoord0);
	oTexcoord2 = mad(_MainTex_TexelSize.xyxy, float4( 1.0, 0.0, 0.0,  1.0), oTexcoord0);
	oTexcoord3 = mad(_MainTex_TexelSize.xyxy, float4(-2.0, 0.0, 0.0, -2.0), oTexcoord0);
	return Position;
}

float4 SMAALumaEdgeDetectionPS(
	in float4 coord0 : TEXCOORD0,
	in float4 coord1 : TEXCOORD1,
	in float4 coord2 : TEXCOORD2,
	in float4 coord3 : TEXCOORD3,
	uniform sampler _MainTex) : COLOR
{
	float2 threshold = float2(SMAA_THRESHOLD, SMAA_THRESHOLD);

	float3 weights = float3(0.2126, 0.7152, 0.0722);
	float Lcenter   = dot(tex2Dlod(_MainTex, float4(coord0.xy, 0, 0)).rgb, weights);
	float Lleft     = dot(tex2Dlod(_MainTex, float4(coord1.xy, 0, 0)).rgb, weights);
	float Ltop      = dot(tex2Dlod(_MainTex, float4(coord1.zw, 0, 0)).rgb, weights);

	float4 delta = 0;
	delta.xy = abs(Lcenter - float2(Lleft, Ltop));

	float2 edges = step(threshold, delta.xy);
    if (dot(edges, float2(1.0, 1.0)) == 0.0)
        discard;

	float Lright    = dot(tex2Dlod(_MainTex, float4(coord2.xy, 0, 0)).rgb, weights);
	float Lbottom   = dot(tex2Dlod(_MainTex, float4(coord2.zw, 0, 0)).rgb, weights);
	float Lleftleft = dot(tex2Dlod(_MainTex, float4(coord3.xy, 0, 0)).rgb, weights);
	float Ltoptop   = dot(tex2Dlod(_MainTex, float4(coord3.zw, 0, 0)).rgb, weights);

    delta.zw = abs(Lcenter - float2(Lright, Lbottom));

	float2 maxDelta = max(delta.xy, delta.zw);
	maxDelta = max(maxDelta.xy, abs(float2(Lleft, Ltop) - float2(Lleftleft, Ltoptop)));

	return float4(edges * step(max(maxDelta.x, maxDelta.y), 2.0 * delta.xy), 0.0, 0.0);
}

float4 SMAABlendingWeightCalculationVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float4 oTexcoord2 : TEXCOORD2,
	out float4 oTexcoord3 : TEXCOORD3,
	uniform float4 _MainTex_TexelSize) : POSITION
{
	float2 coord = Texcoord.xy + _MainTex_TexelSize.xy * 0.5;
	oTexcoord0 = float4(coord.xy, Texcoord.xy) * float4(1.0, 1.0, _MainTex_TexelSize.zw);
	oTexcoord1 = mad(_MainTex_TexelSize.xyxy, float4(-0.25, -0.125,  1.25, -0.125), coord.xyxy);
	oTexcoord2 = mad(_MainTex_TexelSize.xyxy, float4(-0.125, -0.25, -0.125,  1.25), coord.xyxy);
	oTexcoord3 = mad(_MainTex_TexelSize.xxyy, float4(-2.0, 2.0, -2.0, 2.0) * float(SMAA_MAX_SEARCH_STEPS), float4(oTexcoord1.xz, oTexcoord2.yw));
	return Position;
}

float4 SMAABlendingWeightCalculationPS(
	in float4 uv0 : TEXCOORD0,
	in float4 uv1 : TEXCOORD1,
	in float4 uv2 : TEXCOORD2,
	in float4 uv3 : TEXCOORD3,
	uniform float4 subsampleIndices,
	uniform float4 _MainTex_TexelSize) : COLOR
{
	float4 weights = 0;
	float4 offset[3] = { uv1, uv2, uv3 };
	float2 edge = tex2Dlod(_SMAAEdgeMap_LinearSampler, float4(uv0.xy, 0, 0)).rg;

	[branch]
	if (edge.g > 0.0)
	{
#if SMAA_MAX_SEARCH_STEPS_DIAG
		weights.rg = SMAACalculateDiagWeights(_SMAAEdgeMap_LinearSampler, _SMAAAreaMap_PointSampler, uv0.xy, _MainTex_TexelSize.xy, edge, subsampleIndices);

		[branch]
		if (weights.r == -weights.g) 
		{
#endif
		
		float3 coords;
		coords.x = SMAASearchXLeft(_SMAAEdgeMap_LinearSampler, _SMAASearchMap_PointSampler, offset[0].xy, offset[2].x, _MainTex_TexelSize.xy);
		coords.y = offset[1].y;
		coords.z = SMAASearchXRight(_SMAAEdgeMap_LinearSampler, _SMAASearchMap_PointSampler, offset[0].zw, offset[2].y, _MainTex_TexelSize.xy);
		
		float2 d = coords.xz;
		d = abs(round(mad(_MainTex_TexelSize.zz, d, -uv0.zz)));
		
		float e1 = tex2Dlod(_SMAAEdgeMap_LinearSampler, float4(coords.xy, 0, 0)).r;
		float e2 = tex2Dlod(_SMAAEdgeMap_LinearSampler, float4(coords.zy + float2(_MainTex_TexelSize.x, 0), 0, 0)).r;
		
		weights.rg = SMAAArea(_SMAAAreaMap_PointSampler, sqrt(d), e1, e2, subsampleIndices.y);
		
		coords.y = uv0.y;
		SMAADetectHorizontalCornerPattern(_SMAAEdgeMap_LinearSampler, weights.rg, coords.xyzy, d, _MainTex_TexelSize.xy);
		
#if SMAA_MAX_SEARCH_STEPS_DIAG
		} 
		else
		{
			edge.r = 0.0;
		}
#endif
	}

	[branch]
	if (edge.r > 0.0)
	{
		float3 coords;
		coords.y = SMAASearchYUp(_SMAAEdgeMap_LinearSampler, _SMAASearchMap_PointSampler, offset[1].xy, offset[2].z, _MainTex_TexelSize.xy);
		coords.x = offset[0].x;
		coords.z = SMAASearchYDown(_SMAAEdgeMap_LinearSampler, _SMAASearchMap_PointSampler, offset[1].zw, offset[2].w, _MainTex_TexelSize.xy);
		
		float2 d = coords.yz;
		d = abs(round(mad(_MainTex_TexelSize.ww, d, -uv0.ww)));

		float e1 = tex2Dlod(_SMAAEdgeMap_LinearSampler, float4(coords.xy, 0, 0)).g;
		float e2 = tex2Dlod(_SMAAEdgeMap_LinearSampler, float4(coords.xz + float2(0, _MainTex_TexelSize.y), 0, 0)).g;
		
		weights.ba = SMAAArea(_SMAAAreaMap_PointSampler, sqrt(d), e1, e2, subsampleIndices.x);
		
		coords.x = uv0.x;
		SMAADetectVerticalCornerPattern(_SMAAEdgeMap_LinearSampler, weights.ba, coords.xyxz, d, _MainTex_TexelSize.xy);
	}

	return weights;
}

float4 SMAANeighborhoodBlendingVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	uniform float2 _MainTex_TexelSize) : POSITION
{
	oTexcoord0 = Texcoord.xyxy + _MainTex_TexelSize.xyxy * 0.5;
	oTexcoord1 = mad(_MainTex_TexelSize.xyxy, float4(1.0, 0.0, 0.0, 1.0), oTexcoord0);
	return Position;
}

float4 SMAANeighborhoodBlendingPS(
	in float2 uv0 : TEXCOORD0,
	in float4 uv1 : TEXCOORD1,
	uniform sampler _MainTex,
	uniform float2 _MainTex_TexelSize,
	uniform bool final) : COLOR
{
	float4 a;
	a.x = tex2Dlod(_SMAABlendMap_PointSampler, float4(uv1.xy, 0, 0)).a;
	a.y = tex2Dlod(_SMAABlendMap_PointSampler, float4(uv1.zw, 0, 0)).g;
	a.wz = tex2Dlod(_SMAABlendMap_PointSampler, float4(uv0, 0, 0)).xz;

	[branch]
	if (dot(a, float4(1.0, 1.0, 1.0, 1.0)) < 1e-5) 
	{
		float4 color = tex2Dlod(_MainTex, float4(uv0, 0, 0));
		return float4(color.rgb, final ? 1 : luminance(color.rgb));
	}
	else 
	{
		bool h = max(a.x, a.z) > max(a.y, a.w);

		float4 blendingOffset = float4(0.0, a.y, 0.0, a.w);
		float2 blendingWeight = a.yw;
		SMAAMovc(bool4(h, h, h, h), blendingOffset, float4(a.x, 0.0, a.z, 0.0));
		SMAAMovc(bool2(h, h), blendingWeight, a.xz);
		blendingWeight /= sum(blendingWeight);

		float4 blendingCoord = mad(blendingOffset, float4(_MainTex_TexelSize.xy, -_MainTex_TexelSize.xy), uv0.xyxy);

		float4 color = blendingWeight.x * tex2Dlod(_MainTex, float4(blendingCoord.xy, 0, 0));
		color += blendingWeight.y * tex2Dlod(_MainTex, float4(blendingCoord.zw, 0, 0));

		return float4(color.rgb, final ? 1 : luminance(color.rgb));
	}
}