#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/Color.fxsub"
#include "../../../shader/Packing.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/BRDF.fxsub"
#include "../../../shader/arealight.fxsub"

#if SHADOW_MAP_FROM > 0
#	include "../../../shader/Poisson.fxsub"
#	include "../../../shader/Shadow.fxsub"
#endif

#define TUBELIGHT_MAX_WIDTH 50
#define TUBELIGHT_MAX_RADIUS 10

float mHP : CONTROLOBJECT<string name = "(self)"; string item = "H+";>;
float mSP : CONTROLOBJECT<string name = "(self)"; string item = "S+";>;
float mVP : CONTROLOBJECT<string name = "(self)"; string item = "V+";>;
float mVM : CONTROLOBJECT<string name = "(self)"; string item = "V-";>;
float mBlink : CONTROLOBJECT<string name="(self)"; string item = "Blink";>;
float mWidthP : CONTROLOBJECT<string name="(self)"; string item = "Width+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item = "Range+";>;
float mRangeM : CONTROLOBJECT<string name="(self)"; string item = "Range-";>;
float mRadiusM : CONTROLOBJECT<string name="(self)"; string item = "Radius-";>;
float mRadiusP : CONTROLOBJECT<string name="(self)"; string item = "Radius+";>;
float mHardnessP : CONTROLOBJECT<string name="(self)"; string item = "Hardness";>;
float mTemperatureP : CONTROLOBJECT<string name = "(self)"; string item = "Temperature+";>;
float mTemperatureM : CONTROLOBJECT<string name = "(self)"; string item = "Temperature-";>;
float mAttenuationBulbP : CONTROLOBJECT<string name="(self)"; string item = "AttenuationBulb+";>;
float mAttenuationBulbM : CONTROLOBJECT<string name="(self)"; string item = "AttenuationBulb-";>;
float mMultiLightP : CONTROLOBJECT<string name="ray_controller.pmx"; string item = "MultiLight+";>;
float mMultiLightM : CONTROLOBJECT<string name="ray_controller.pmx"; string item = "MultiLight-";>;

float3 mLeft : CONTROLOBJECT<string name="(self)"; string item = "Left";>;
float3 mRight : CONTROLOBJECT<string name="(self)"; string item = "Right";>;
float3 mPosition : CONTROLOBJECT<string name="(self)"; string item = "Position";>;

static const float LightWidth = (2 + mWidthP * TUBELIGHT_MAX_WIDTH);
static const float LightRadius = lerp(lerp(1, TUBELIGHT_MAX_RADIUS, mRadiusP), 0.1, mRadiusM);
static const float LightBlink = saturate(1 - SmoothTriangleWave(time * (mBlink * 10)));
static const float LightRange = lerp(lerp(lightRangeParams.x, lightRangeParams.z, mRangeP), lightRangeParams.y, mRangeM);
static const float LightAttenuationBulb = lerp(lerp(lightAttenuationBulbParams.x, lightAttenuationBulbParams.z, mAttenuationBulbP), lightAttenuationBulbParams.y, mAttenuationBulbM);
static const float LightIntensity = lerp(lerp(lightIntensityParams.x, lightIntensityParams.z, mVP), lightIntensityParams.y, mVM);
static const float LightIntensity2 = lerp(lerp(mLightIntensityMin, mLightIntensityMax, mMultiLightP), 0, mMultiLightM);
static const float LightTemperature = lerp(lerp(lightTemperatureLimits.x, lightTemperatureLimits.z, mTemperatureP), lightTemperatureLimits.y, mTemperatureM);

static const float3 LightPosition = mPosition;
static const float3 LightLeft = mLeft - (mLeft - LightPosition) * LightWidth;
static const float3 LightRight = mRight + (mLeft - LightPosition) * LightWidth;
static const float3 LightColor = float3(mHP, mSP, LightIntensity * LightIntensity2 * LightBlink);

static const float3 viewLightLeft = mul(float4(LightLeft, 1), matView).xyz;
static const float3 viewLightRight = mul(float4(LightRight, 1), matView).xyz;

#if SHADOW_MAP_FROM > 0
texture ShadowMap: OFFSCREENRENDERTARGET <
	string Description = "Shadow Rendering for TubeLight";
	int2 Dimensions = {MULTI_SHADOW_MAP_SIZE * 2, MULTI_SHADOW_MAP_SIZE};
	string Format = "R32F";
	float4 ClearColor = { 1, 1, 1, 1 };
	float ClearDepth = 0.0;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller.pmx=hide;"
		"*editor*.pmx=hide;"
		"*.pmx = ../tube_shadow.fx;"
		"*.pmd = ../tube_shadow.fx;"
		"* = hide";
>;
sampler ShadowMapSamp = sampler_state {
	texture = <ShadowMap>;
	MinFilter = POINT;	MagFilter = LINEAR;	MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

static float2 ShadowMapOffset  = 0.5 / float2(MULTI_SHADOW_MAP_SIZE * 2, MULTI_SHADOW_MAP_SIZE);
static float2 ShadowMapOffset2 = 1.0 / float2(MULTI_SHADOW_MAP_SIZE * 2, MULTI_SHADOW_MAP_SIZE);

#if defined(MULTI_SHADOW_PCSS)
float2 FindBlocker(sampler _ShadowTex, float2 uv, float depth, float2 searchRadius, float2 rotation)
{
    float blockerWeight = 0;
    float blockerDepthSum = 0.0;

    [loop]
    for (int i = 0; i < MULTI_SHADOW_BLOCKER_COUNT; i++)
    {
		float sampleDepth = 1 - tex2Dlod(_ShadowTex, float4(uv + Rotate(MULTI_SHADOW_BLOCKER_POISSON[i], rotation) * searchRadius, 0, 0)).r;
        if (sampleDepth < depth)
        {
            blockerDepthSum += sampleDepth;
            blockerWeight++;
        }
    }

    return float2(blockerDepthSum / max(1e-5, blockerWeight), blockerWeight);
}
#endif

float ShadingShadow(float3 viewPosition, float2 radius, float random)
{
	float2 rotation = float2(cos(random * PI_2), sin(random * PI_2));

	float3 P = mul(float4(viewPosition, 1), matViewInverse).xyz;
	float3 L = P - LightPosition;

	float linearDepth = length(L);
	float receiverDepth = GetDeviceDepth(linearDepth, mPointLightNear, mPointLightFar);
	receiverDepth -= 1e-4 * linearDepth;

	float2 uv = CalcDualShadowCoord(L / linearDepth, MULTI_SHADOW_MAP_SIZE) + ShadowMapOffset;

#if defined(MULTI_SHADOW_PCSS)
	float2 searchRegionRadius = SearchRegionRadius(radius, linearDepth, mPointLightNear);
	float2 blocker = FindBlocker(ShadowMapSamp, uv, receiverDepth, searchRegionRadius, rotation);

	float2 penumbraRatio = PenumbraRadius(radius, receiverDepth, GetLinearDepth(blocker.x, mPointLightNear * 0.5, mPointLightFar));
	float2 filterSize = ProjectToLight(penumbraRatio, receiverDepth, mPointLightNear);

	[branch]
	if (blocker.y <= 0)
	{
		return 1;
	}
#else
	float2 filterSize = radius * 0.5;
#endif

	float shadow = 0;

	[unroll]
	for (int i = 0; i < MULTI_SHADOW_POISSON_COUNT; i+=4)
	{
		float2 samplePoints[4];
		samplePoints[0] = Rotate(MULTI_SHADOW_POISSON[i], rotation);
		samplePoints[1] = Rotate(MULTI_SHADOW_POISSON[i+1], rotation);
		samplePoints[2] = Rotate(MULTI_SHADOW_POISSON[i+2], rotation);
		samplePoints[3] = Rotate(MULTI_SHADOW_POISSON[i+3], rotation);

		float4 sampleDepth;
		sampleDepth[0] = tex2Dlod(ShadowMapSamp, float4(uv + samplePoints[0] * filterSize, 0, 0)).r;
		sampleDepth[1] = tex2Dlod(ShadowMapSamp, float4(uv + samplePoints[0] * filterSize, 0, 0)).r;
		sampleDepth[2] = tex2Dlod(ShadowMapSamp, float4(uv + samplePoints[0] * filterSize, 0, 0)).r;
		sampleDepth[3] = tex2Dlod(ShadowMapSamp, float4(uv + samplePoints[0] * filterSize, 0, 0)).r;

		sampleDepth = 1 - sampleDepth;

		shadow += receiverDepth < sampleDepth.x ? 1: 0;
		shadow += receiverDepth < sampleDepth.y ? 1: 0;
		shadow += receiverDepth < sampleDepth.z ? 1: 0;
		shadow += receiverDepth < sampleDepth.w ? 1: 0;
	}

	return shadow / MULTI_SHADOW_POISSON_COUNT;
}
#endif

void ShadingMaterial(MaterialParam material, float3 P, float3 V, float3 lightColor, inout float3 diffuse, inout float3 specular)
{
	float3 L0 = viewLightLeft - P;
	float3 L1 = viewLightRight - P;
	float3 Lw = TubeLightDirection(material.normal, V, L0, L1, P, LightRadius);
	float3 L = normalize(Lw);

	float3 atten = GetPhysicalLightAttenuation(Lw, LightRange, LightAttenuationBulb);
	atten *= lightColor;

	float2x3 energy = TubeLightEnergy(material, P, material.normal, V, L, L0, L1, LightWidth, LightRadius);
	diffuse = energy[0];
	diffuse *= atten;

#if LIGHT_PARAMS_TYPE == 0
	specular = energy[1];
	specular *= atten;
#endif
}

void GetGbufferParams(float4 texcoord, out MaterialParam material, out MaterialParam materialAlpha)
{
	float2 coord = texcoord.xy / texcoord.w;

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);

	clip(sum(materialAlpha.albedo + materialAlpha.specular) - 1e-5);

	float4 MRT1 = tex2Dlod(Gbuffer1Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer2Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer3Map, float4(coord, 0, 0));
	float4 MRT4 = tex2Dlod(Gbuffer4Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
}

void LightSourceVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float3 oTexcoord2 : TEXCOORD2,
	out float4 oPosition  : POSITION)
{
	Position.xyz = LightPosition + (Position.xyz - LightPosition) * LightRange * 100;
	oTexcoord1 = -mul(Position, matView);
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
	oTexcoord2 = hsv2rgb(LightColor);
	oTexcoord2 = ColorTemperature(oTexcoord2, LightTemperature);
}

void LightSourcePS(
	in float4 texcoord : TEXCOORD0,
	in float3 viewdir : TEXCOORD1,
	in float3 lightColor : TEXCOORD2,
	in float2 screen : SV_POSITION,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);

	float3 P1 = V * material.linearDepth / V.z;
	float3 P2 = V * materialAlpha.linearDepth / V.z;

	float3 diffuse, specular = 0;
	ShadingMaterial(material, P1, V, lightColor, diffuse, specular);

	float3 diffuseAlpha, specularAlpha = 0;
	ShadingMaterial(materialAlpha, P2, V, lightColor, diffuseAlpha, specularAlpha);

	oColor0 = float4(diffuse, 0);
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);

#if SHADOW_MAP_FROM > 0
	clip(sum(oColor0 + oColor1 + oColor2) - 1e-5);

	float radius = clamp(LightWidth * LightRadius / 1024, lightRadiusLimits.y, lightRadiusLimits.z);
	float shadow = ShadingShadow(P2, radius * float2(1.0, 0.5), PseudoRandom(screen));

	oColor0 *= shadow;
	oColor1 *= shadow;
	oColor2 *= shadow;
#endif
}

void LightSourceShadowlessPS(
	in float4 texcoord : TEXCOORD0,
	in float3 viewdir : TEXCOORD1,
	in float3 lightColor : TEXCOORD2,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);

	float3 P1 = V * material.linearDepth / V.z;
	float3 P2 = V * materialAlpha.linearDepth / V.z;

	float3 diffuse, specular = 0;
	ShadingMaterial(material, P1, V, lightColor, diffuse, specular);

	float3 diffuseAlpha, specularAlpha = 0;
	ShadingMaterial(materialAlpha, P2, V, lightColor, diffuseAlpha, specularAlpha);

	oColor0 = float4(diffuse, 0);
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);
}

technique MainTech0<string MMDPass = "object";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourceShadowlessPS();
	}
}

technique MainTecBS0<string MMDPass = "object_ss";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourcePS();
	}
}

technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}